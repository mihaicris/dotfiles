#!/usr/bin/env zsh

heading "Switch branch git repo and its submodules"

is_git_folder || exit 1

function switch_branch {
    REPO_PATH=$1
    NEW_BRANCH=$2
    CURRENT_BRANCH=$(git -C $REPO_PATH branch --show-current)

    [[ -d $REPO_PATH ]] || { echo "Repo path not valid!"; return 1 ; }
    [[ $NEW_BRANCH ]] || { echo "Branch name is empty!"; return 1 ; }

    git -C $REPO_PATH switch --guess --quiet $NEW_BRANCH

    if (( $? == 0 )); then
        print "Switched to branch: ${GREEN}${NEW_BRANCH}${NORMAL}"
    else
        if [[ $CURRENT_BRANCH ]]; then
            print "On branch: ${LIGHT_RED}${CURRENT_BRANCH}${NORMAL}"
        else
            print "On branch: ${LIGHT_RED}Detached state${NORMAL}"
        fi
    fi
    print ""
}

TOP_LEVEL_DIR=$(git rev-parse --show-toplevel)
BRANCH=${1:-apimaindevelopment}

SUBMODULES=$(git -C $TOP_LEVEL_DIR config \
                 --file $TOP_LEVEL_DIR/.gitmodules \
                 --get-regexp path \
             | awk -v path=${TOP_LEVEL_DIR}/ '{ print path$2 }')

REPOS=($TOP_LEVEL_DIR ${(f)SUBMODULES})

for REPO in $REPOS; do
    print ${UNDERLINE}${BOLD}${BLUE}${REPO}${NORMAL}
    switch_branch $REPO $BRANCH
done
